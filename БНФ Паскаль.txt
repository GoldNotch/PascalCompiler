<программа>::=program <имя>(<имя файла>{,<имя файла>});<блок>.
<имя файла>::=<имя>
<имя>::=<буква>{<буква>|<цифра>}
<блок>::=<раздел меток><раздел констант><раздел типов><раздел переменных><раздел процедур и функций><раздел операторов>
<раздел меток>::=<пусто>|label <метка>{,<метка>};
<метка>::=<целое без знака>
<раздел констант>::=<пусто>|const <определение константы>;{<определение константы>;}<определение константы>::=<имя>=<константа>
<константа>::=<число без знака>|<знак><число без знака>|
<имя константы>|<знак><имя константы>|<строка>
<число без знака>::=<целое без знака>|<вещественное без знака>
<целое без знака>::=<цифра>{<цифра>}
<вещественное без знака>::=<целое без знака>.<цифра>{<цифра>}|<целое без знака>.<цифра>{<цифра>}E<порядок>|<целое без знака>E<порядок>
<порядок>::=<целое без знака>|<знак><целое без знака>
<знак>::=+|-
<имя константы>::=<имя>
<строка>::='<символ>{<символ>}'
<раздел типов>::=<пусто>|type <определение типа>;{<определение типа>;}
<определение типа>::=<имя>=<тип>
<тип>::=<простой тип>|<составной тип>|<ссылочный тип>
<простой тип>::=<перечислимый тип>|<ограниченный тип>|<имя типа>
<перечислимый тип>::=(<имя>{,<имя>})
<ограниченный тип>::=<константа>..<константа>
<имя типа>::=<имя>
<составной тип>::=<неупакованный составной тип>|packed <неупакованный составной тип>
<неупакованный составной тип>::=<регулярный тип>|<комбинированный тип>|<множественный тип>|<файловый тип>
<регулярный тип>::= array[<простой тип>{,<простой тип>}] of <тип компоненты >
<тип компоненты>::=<тип>
<комбинированный тип>::=record <список полей> end
<список полей>::=<фиксированная часть>|<фиксированная часть><вариантная часть>|<вариантная часть>
<фиксированная часть>::=<секция записи>{;<секция записи>}
<секция записи>::=<имя поля>{,<имя поля>}:<тип>|<пусто>
<вариантная часть>::= case <поле признака><имя типа> of <вариант>{;<вариант>}
<поле признака>::=<имя поля>:|<пусто>
<вариант>::=<список меток варианта>:(<список полей>)|<пусто>
<список меток варианта>::=<метка варианта>{,<метка варианта>}
<метка варианта>::=<константа>
<множественный тип>::= set of <базовый тип>
<базовый тип>::=<простой тип>
<файловый тип>::= file of <тип>
<ссылочный тип>::=^<имя типа>
<раздел переменных>::= var <описание однотипных переменных>;{<описание однотипных переменных>;}|<пусто>
<описание однотипных переменных>::=<имя>{,<имя>}:<тип>
<раздел процедур и функций>::={<описание процедуры или функции>;}
<описание процедуры или функции>::=<описание процедуры>|<описание функции>
<описание процедуры>::=<заголовок процедуры><блок>
<заголовок процедуры>::= procedure <имя>;| procedure <имя>(<раздел формальных параметров>{;<раздел формальных параметров>});
<раздел формальных параметров>::=<группа параметров>|var <группа параметров>| function <группа параметров>|procedure <имя>{,<имя>}
<группа параметров>::=<имя>{,<имя>}:<имя типа>
<описание функции>::=<заголовок функции><блок>
<заголовок функции>::= function <имя>:<тип результата>;|function <имя>(<раздел формальных параметров>{;<раздел формальных параметров>}):<тип результата>;
<тип результата>::=<имя типа>
<раздел операторов>::=<составной оператор>
<оператор>::=<непомеченный оператор>|<метка><непомеченный оператор>
<непомеченный оператор>::=<простой оператор>|<сложный оператор>
<простой оператор>::=<оператор присваивания>|<оператор процедуры>|<оператор перехода>|<пустой оператор>
<оператор присваивания>::=<переменная>:=<выражение>|<имя функции>:=<выражение>
<переменная>::=<полная переменная>|<компонента переменной>|<указанная переменная>
<полная переменная>::=<имя переменной>
<имя переменной>::=<имя>
<компонента переменной>::=<индексированная переменная>|<обозначение поля>|<буфер файла>
<индексированная переменная>::=<переменная-массив>[<выражение>{,<выражение>}]
<переменная-массив>::=<переменная>
<обозначение поля>::=<переменная-запись>.<имя поля>
<переменная-запись>::=<переменная>
<имя поля>::=<имя>
<буфер файла>::=<переменная-файл>^
<переменная-файл>::=<переменная>
<указанная переменная>::=<переменная-ссылка>^
<переменная-ссылка>::=<переменная>
<выражение>::=<простое выражение>|<простое выражение><операция отношения><простое выражение>
<операция отношения>::==|<>|<|<=|>=|>|in
<простое выражение>::=<знак><слагаемое>{<аддитивная операция><слагаемое>}
<аддитивная операция>::=+|-|or
<слагаемое>::=<множитель>{<мультипликативная операция><множитель>}
<мультипликативная операция>::=*|/|div|mod|and
<множитель>::=<переменная>|<константа без знака>|(<выражение>)|<обозначение функции>|<множество>|not <множитель>
<константа без знака>::=<число без знака>|<строка>|<имя константы>|nil
<обозначение функции>::=<имя функции>|<имя функции>(<фактический параметр>{,<фактический параметр>})
<имя функции>::=<имя>
<множество>::=[<список элементов>]
<список элементов>::=<элемент>{,<элемент>}|<пусто>
<элемент>::=<выражение>|<выражение>..<выражение>
<оператор процедуры>::=<имя процедуры>|<имя процедуры>(<фактический параметр>{,<фактический параметр>})
<имя процедуры>::=<имя>
<фактический параметр>::=<выражение>|<переменная>|<имя процедуры>|<имя функции>
<оператор перехода>::= goto <метка>
<пустой оператор>::=<пусто>
<пусто>::=
<сложный оператор>::=<составной оператор>|<выбирающий оператор>|<оператор цикла>|<оператор присоединения>
<составной оператор>::= begin <оператор>{;<оператор>} end
<выбирающий оператор>::=<условный оператор>|<оператор варианта>
<условный оператор>::= if <выражение> then <оператор>|if <выражение> then <оператор> else <оператор>
<оператор варианта>::= case <выражение> of <элемент списка вариантов>{;<элемент списка вариантов>} end
<элемент списка вариантов>::=<список меток варианта>:<оператор>|<пусто>
<список меток варианта>::=<метка варианта>{,<метка варианта>}
<оператор цикла>::=<цикл с предусловием>|<цикл с постусловием>|<цикл с параметром>
<цикл с предусловием>::= while <выражение> do <оператор>
<цикл с постусловием>::= repeat <оператор>{;<оператор>} until <выражение>
<цикл с параметром>::= for <параметр цикла>:=<выражение><направление><выражение> do <оператор><параметр цикла>:=<имя>
<направление>::= to|downto
<оператор присоединения>::= with <список переменных-записей> do <оператор>
<список переменных-записей>::=<переменная-запись>{,<переменная-запись>}